plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.devtools.ksp'
}

android {
    namespace 'com.mo.sh.studyassistant'
    compileSdk 34

    defaultConfig {
        applicationId "com.mo.sh.studyassistant"
        minSdk 26
        targetSdk 34
        versionCode 2
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
        benchmark {
            initWith release
            signingConfig signingConfigs.debug
            matchingFallbacks = ['release']
            debuggable false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    androidResources {
        generateLocaleConfig true
    }
}

dependencies {
    def room_version = "2.5.2"
    def nav_version = "2.7.3"
    def lifecycle_version = "2.6.2"
    def ktor_version = "2.0.0"

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.9.0')
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation platform('androidx.compose:compose-bom:2023.09.02')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    // Hilt
    implementation "com.google.dagger:hilt-android:2.48"
    ksp "com.google.dagger:hilt-compiler:2.48"

    // Navigation
    implementation "androidx.navigation:navigation-compose:$nav_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // ViewModel utilities for Compose
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

    // ML Kit
    implementation 'com.google.mlkit:text-recognition:16.0.0'

    // Accompanist libraries
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.23.1"

    // PDFBox
    implementation 'com.tom-roush:pdfbox-android:2.0.27.0'

    //Coil
    implementation "io.coil-kt:coil-compose:2.4.0"

    //DocumentFile
    implementation "androidx.documentfile:documentfile:1.0.1"

    // Image cropper
    implementation "com.vanniktech:android-image-cropper:4.5.0"

    // Ktor
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"
    implementation "io.ktor:ktor-client-logging:$ktor_version"
    implementation "ch.qos.logback:logback-classic:1.2.10"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0'

    // Compose markdown
    implementation 'com.github.jeziellago:compose-markdown:0.3.4'

    // DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Squircle shape
    implementation "com.github.stoyan-vuchev:squircle-shape:1.0.3"

    // Baseline profiler
    implementation "androidx.profileinstaller:profileinstaller:1.3.1"
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas")
}